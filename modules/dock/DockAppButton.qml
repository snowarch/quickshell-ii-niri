import qs.services
import qs.modules.common
import qs.modules.common.functions
import Qt5Compat.GraphicalEffects
import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Widgets

DockButton {
    id: root
    property var appToplevel
    property var appListRoot
    property int lastFocused: -1
    property real iconSize: Config.options.dock.iconSize ?? 35
    property real countDotWidth: 10
    property real countDotHeight: 4
    property bool appIsActive: appToplevel.toplevels.find(t => (t.activated == true)) !== undefined
    property bool hasWindows: appToplevel.toplevels.length > 0
    property bool buttonHovered: false
    
    // Subtle highlight for active app
    scale: appIsActive ? 1.05 : 1.0
    Behavior on scale { NumberAnimation { duration: 150; easing.type: Easing.OutQuad } }

    property bool isSeparator: appToplevel.appId === "SEPARATOR"
    // Use originalAppId (preserves case) for desktop entry lookup, fallback to appId for backwards compat
    property var desktopEntry: DesktopEntries.heuristicLookup(appToplevel.originalAppId ?? appToplevel.appId)
    enabled: !isSeparator
    implicitWidth: isSeparator ? 1 : implicitHeight - topInset - bottomInset

    Loader {
        active: isSeparator
        anchors {
            fill: parent
            topMargin: dockVisualBackground.margin + dockRow.padding + Appearance.rounding.normal
            bottomMargin: dockVisualBackground.margin + dockRow.padding + Appearance.rounding.normal
        }
        sourceComponent: DockSeparator {}
    }

    Loader {
        anchors.fill: parent
        active: appToplevel.toplevels.length > 0
        sourceComponent: MouseArea {
            id: mouseArea
            anchors.fill: parent
            hoverEnabled: true
            acceptedButtons: Qt.NoButton
            onEntered: {
                root.buttonHovered = true
                appListRoot.lastHoveredButton = root
                appListRoot.buttonHovered = true
                lastFocused = appToplevel.toplevels.length - 1
            }
            onExited: {
                root.buttonHovered = false
                if (appListRoot.lastHoveredButton === root) {
                    appListRoot.buttonHovered = false
                }
            }
        }
    }

    function launchFromDesktopEntry() {
        // Intentar siempre vía gtk-launch y, si falla, ejecutar appId directamente
        var id = appToplevel.originalAppId ?? appToplevel.appId;
        // Caso especial: YouTube Music (pear)
        if (id === "com.github.th_ch.youtube_music") {
            id = "pear-desktop";
        }
        // Caso especial: Spotify launcher
        if (id === "spotify" || id === "spotify-launcher") {
            id = "spotify-launcher";
        }
        if (id && id !== "" && id !== "SEPARATOR") {
            const cmd = "gtk-launch \"" + id + "\" || \"" + id + "\" &";
            Quickshell.execDetached(["bash", "-lc", cmd]);
            return true;
        }
        return false;
    }

    onClicked: {
        // Sin ventanas abiertas: lanzar nueva instancia desde desktop entry o fallbacks
        if (appToplevel.toplevels.length === 0) {
            launchFromDesktopEntry();
            return;
        }
        // Con ventanas: rotar foco entre instancias abiertas
        lastFocused = (lastFocused + 1) % appToplevel.toplevels.length
        appToplevel.toplevels[lastFocused].activate()
    }

    middleClickAction: () => {
        launchFromDesktopEntry();
    }

    altAction: () => {
        // Use originalAppId to ensure proper matching with config (case-sensitive comparison)
        const appId = appToplevel.originalAppId ?? appToplevel.appId;
        if (Config.options.dock.pinnedApps.indexOf(appId) !== -1) {
            Config.options.dock.pinnedApps = Config.options.dock.pinnedApps.filter(id => id !== appId)
        } else {
            Config.options.dock.pinnedApps = Config.options.dock.pinnedApps.concat([appId])
        }
    }

    contentItem: Loader {
        active: !isSeparator
        sourceComponent: Item {
            anchors.centerIn: parent

            Loader {
                id: iconImageLoader
                anchors {
                    left: parent.left
                    right: parent.right
                    verticalCenter: parent.verticalCenter
                }
                active: !root.isSeparator
                sourceComponent: IconImage {
                    // Use desktop entry icon if available, fallback to guessed icon
                    source: {
                        const appId = appToplevel.originalAppId ?? appToplevel.appId;
                        let iconName;

                        // Caso especial: Spotify → usar icono de tema "spotify"
                        if (appId === "Spotify" || appId === "spotify" || appId === "spotify-launcher") {
                            iconName = "spotify";
                        } else {
                            iconName = root.desktopEntry?.icon || AppSearch.guessIcon(appId);
                        }

                        return Quickshell.iconPath(iconName, "image-missing");
                    }
                    implicitSize: root.iconSize
                }
            }

            Loader {
                active: Config.options.dock.monochromeIcons
                anchors.fill: iconImageLoader
                sourceComponent: Item {
                    Desaturate {
                        id: desaturatedIcon
                        visible: false // There's already color overlay
                        anchors.fill: parent
                        source: iconImageLoader
                        desaturation: 0.8
                    }
                    ColorOverlay {
                        anchors.fill: desaturatedIcon
                        source: desaturatedIcon
                        color: ColorUtils.transparentize(Appearance.colors.colPrimary, 0.9)
                    }
                }
            }

            // Indicator: line for active, dot for inactive with windows
            Loader {
                active: root.hasWindows && !root.isSeparator
                anchors {
                    top: iconImageLoader.bottom
                    topMargin: 2
                    horizontalCenter: parent.horizontalCenter
                }
                sourceComponent: Row {
                    spacing: 3
                    // Active app: show line indicators for each window
                    Repeater {
                        model: root.appIsActive ? Math.min(appToplevel.toplevels.length, 3) : 0
                        delegate: Rectangle {
                            radius: Appearance.rounding.full
                            implicitWidth: (appToplevel.toplevels.length <= 3) ? root.countDotWidth : root.countDotHeight
                            implicitHeight: root.countDotHeight
                            color: Appearance.colors.colPrimary
                        }
                    }
                    // Inactive app with windows: show single dot
                    Rectangle {
                        visible: !root.appIsActive && root.hasWindows
                        width: 5
                        height: 5
                        radius: 2.5
                        color: ColorUtils.transparentize(Appearance.colors.colOnLayer0, 0.5)
                    }
                }
            }
        }
    }
}
